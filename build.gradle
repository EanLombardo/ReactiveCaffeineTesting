buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1+"
        classpath 'org.ajoberstar:gradle-git:1.4.2'
    }
}

def VERSION_CODE = '0.0.1'

group 'com.rxc'
version VERSION_CODE

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'org.ajoberstar.github-pages'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'reactive-caffeine:reactive-caffeine:1.0.0'
    compile 'junit:junit:4.11'
}


bintray {
    user = hasProperty('bintrayUser') ? property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = hasProperty('bintrayApiKey') ? property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    filesSpec {
        from 'build/libs'
        into '.'
    }
    pkg {
        repo = 'maven'
        name = 'ReactiveCaffeineTesting'
        desc = 'Some handy tools for testing RXJava'
        licenses = ['Apache-2.0']
        vcsUrl = 'git@github.com:EanLombardo/ReactiveCaffeineTesting.git'
        labels = ['rx', 'java', 'rxjava', 'junit']
        publicDownloadNumbers = true
        publications = ['MyPublication']
        version {
            name = VERSION_CODE
            desc = ''
            vcsTag = VERSION_CODE
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'reactive-caffeine'
            artifactId 'reactive-caffeine-testing'
            version VERSION_CODE
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

githubPages {
    repoUri = 'git@github.com:EanLombardo/ReactiveCaffeineTesting.git'
    pages {
        from javadoc
    }
}